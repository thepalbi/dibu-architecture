prog: (line EOL)+

line: COMMENT -> comment
    | code

code: label? opcode operands

label: NAME ":"

opcode: OPCODE

OPCODE: "mov"
    | "add"
    | "sub"
    | "and"
    | "or"
    | "lsl"
    | "lsr"
    | "not"

operands: operand+

?operand: REG_NAME  -> register_operand
    | immediate     -> immediate_operand
    | NAME          -> label_operand
    | address

?address: "[" address_mode "]"

?address_mode: REG_NAME -> mem_indirect
    | immediate         -> mem_direct

REG_NAME: "r" DIGIT

immediate: IMM_BINARY -> binary
    | IMM_HEXA        -> hexa
    | IMM_DECI        -> decimal

IMM_BINARY: "0b" ("0" | "1")+
IMM_HEXA: "0x" (DIGIT | "a".."f")+
IMM_DECI: "0d" DIGIT+

EOL: "\n"

NAME: ("a".."z" | "_" | "A".."Z")+

COMMENT: /;[^\n]*/ 
WHITESPACE: " "
%ignore WHITESPACE

%import common.DIGIT
